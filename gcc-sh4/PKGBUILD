# Maintainer: cpasjuste <cpasjuste@gmail.com>
# Credits: https://github.com/KallistiOS/KallistiOS

_koscommit=c84b74b52c9240cd02fe52d5f92d4d256d85ce69
_binutilsver=2.40
_newlibver=4.3.0.20230120

pkgname=dc-gcc-sh4
pkgver=12.2.0
pkgrel=1
pkgdesc='gcc (sh4) for the sega dreamcast'
arch=('any')
url='https://mirror.cyberbits.eu/gnu/gcc/'
license=('GNU GPL')
options=(!strip libtool)
source=(
  "https://mirror.cyberbits.eu/gnu/binutils/binutils-$_binutilsver.tar.bz2"
  "https://mirror.cyberbits.eu/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz"
  "https://sourceware.org/ftp/newlib/newlib-$_newlibver.tar.gz"
  "https://github.com/KallistiOS/KallistiOS/raw/$_koscommit/utils/dc-chain/patches/gcc-$pkgver-kos.diff"
  "https://github.com/KallistiOS/KallistiOS/raw/$_koscommit/utils/dc-chain/patches/newlib-$_newlibver-kos.diff"
  "https://github.com/KallistiOS/KallistiOS/archive/$_koscommit.tar.gz"
)
sha256sums=(
  'f8298eb153a4b37d112e945aa5cb2850040bcf26a3ea65b5a715c83afe05e48a'
  'e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff'
  '83a62a99af59e38eb9b0c58ed092ee24d700fff43a22c03e433955113ef35150'
  'e49ab55961741751ac8f4b0e766414efe71c24ab3c84971b8fd7532f3d98c655'
  'd0c3ccd8cd08f2f29e0fe833ff00016a2a0107e8ada312c843501f2fdb7d05db'
  '802e95acfad50e7c6419814248dbe2a718b784c5182d43b426d545f08f739dfc'
)
groups=('dc-toolchain')

prepare() {
  # apply kos gcc patch
  cd $srcdir/gcc-$pkgver
  patch -p1 < $srcdir/gcc-$pkgver-kos.diff
  
  # apply kos newlib patch
  cd $srcdir/newlib-$_newlibver
  patch -p1 < $srcdir/newlib-$_newlibver-kos.diff
}

build() {
  # sh4 binutils
  cd $srcdir/binutils-$_binutilsver
  mkdir -p build && cd build
  ../configure \
    --prefix=$srcdir/host/sh-elf \
    --target=sh-elf \
    --quiet \
    --disable-werror
  make $MAKEFLAGS
  make install
  
  # sh4-gcc-pass1 (temp gcc for newlib build)
  cd $srcdir/gcc-$pkgver
  ./contrib/download_prerequisites
  mkdir -p build && cd build
  ../configure \
    --prefix=$srcdir/host/sh-elf \
    --target=sh-elf \
    --without-headers \
    --with-newlib \
    --enable-languages=c \
    --disable-libssp \
    --disable-tls \
    --enable-checking=release \
    --with-multilib-list=m4-single-only \
    --with-endian=little \
    --with-cpu=m4-single-only
  make $MAKEFLAGS
  make install

  # sh4-newlib
  export PATH=$PATH:$srcdir/host/sh-elf/bin
  cd $srcdir/newlib-$_newlibver
  mkdir -p build && cd build
  ../configure \
    --prefix=$srcdir/host/sh-elf \
    --target=sh-elf \
    --with-multilib-lle \
    --with-cpu=m4-single-only \
    CC_FOR_TARGET=sh-elf-gcc
  make --quiet $MAKEFLAGS
  make install
  
  # sh4-newlib-fixup
  ln -nsf $srcdir/KallistiOS-$_koscommit/include/pthread.h $srcdir/host/sh-elf/sh-elf/include
	ln -nsf $srcdir/KallistiOS-$_koscommit/include/sys/_pthread.h $srcdir/host/sh-elf/sh-elf/include/sys
	ln -nsf $srcdir/KallistiOS-$_koscommit/include/sys/sched.h $srcdir/host/sh-elf/sh-elf/include/sys
	ln -nsf $srcdir/KallistiOS-$_koscommit/include/kos $srcdir/host/sh-elf/sh-elf/include
	ln -nsf $srcdir/KallistiOS-$_koscommit/kernel/arch/dreamcast/include/arch $srcdir/host/sh-elf/sh-elf/include
	ln -nsf $srcdir/KallistiOS-$_koscommit/kernel/arch/dreamcast/include/dc $srcdir/host/sh-elf/sh-elf/include
  
  # sh4-gcc-pass2
  cd $srcdir/gcc-$pkgver
  rm -rf build && mkdir build && cd build
  ../configure \
    --prefix=$srcdir/host/sh-elf \
    --target=sh-elf \
    --without-headers \
    --with-newlib \
    --disable-libssp \
    --disable-tls \
    --enable-threads=kos \
    --enable-languages=c,c++ \
    --enable-checking=release \
    --with-multilib-list=m4-single-only \
    --with-endian=little \
    --with-cpu=m4-single-only
  make $MAKEFLAGS
  make install
  
  # sh4-newlib-fixup (cleanup)
  rm -f $srcdir/host/sh-elf/sh-elf/include/pthread.h
  rm -f $srcdir/host/sh-elf/sh-elf/include/sys/_pthread.h
  rm -f $srcdir/host/sh-elf/sh-elf/include/sys/sched.h
  rm -f $srcdir/host/sh-elf/sh-elf/include/kos
  rm -f $srcdir/host/sh-elf/sh-elf/include/arch
  rm -f $srcdir/host/sh-elf/sh-elf/include/dc
}

package() {
  # fix prefix
  find ${srcdir}/host -type f -exec grep -I -q . {} \; -print | xargs sed -i "s|${srcdir}|/opt/pacbrew/dc|g"
  
  # "install"
  install -d $pkgdir/opt/pacbrew/dc
  mv ${srcdir}/host $pkgdir/opt/pacbrew/dc
}
