# Maintainer: cpasjuste <cpasjuste@gmail.com>

_koscommit=f07a8b98ec991373c614af0c7b45c3ca5a4eb8f3
_dscommit=5c0bbfba0ea428c5747463b54fa8386f5d5d0aa0

pkgname=dc-kos-ds
pkgver=1
pkgrel=1
pkgdesc='KOS is an unofficial development environment for the SEGA Dreamcast game console (with DreamShell patches).'
arch=('any')
url='https://github.com/KallistiOS/KallistiOS'
license=('MIT')
options=(!strip libtool)
source=(
  "https://github.com/KallistiOS/KallistiOS/archive/$_koscommit.tar.gz"
  "https://github.com/DC-SWAT/DreamShell/raw/$_dscommit/sdk/toolchain/patches/kos.diff"
)
sha256sums=(
  '582f9c38a311346dc62a3cf2d6f2272c76a2ce560a460c240fb14251d571f419'
  'SKIP'
)
depends=('dc-environment')
provides=('dc-kos')
conflicts=('dc-kos')

prepare() {
  # apply ds patch
  cd $srcdir/KallistiOS-$_koscommit
  patch -p1 < $srcdir/kos.diff
}

build() {
  cd $srcdir/KallistiOS-$_koscommit

  # use source dir for building
  export KOS_BASE="$srcdir/KallistiOS-$_koscommit"
  
  # setup PacBrew toolchain
  source /opt/pacbrew/dc/dc-env
  
  # reset PacBrew defines for kos build
  AS="as" CC="gcc" CXX="g++" CPP="cpp" \
    LD="ld" AR="ar" NM="nm" RANLIB="ranlib" STRIP="strip" \
    CFLAGS="" CPPFLAGS="" CXXFLAGS="" LDFLAGS="" \
    make $MAKEFLAGS
}

package() {
  # cleanup
  find $srcdir/KallistiOS-$_koscommit -type f -name "*.o" -exec rm -f {} \;
  
  # fix prefix
  find $srcdir/KallistiOS-$_koscommit -type f -exec grep -I -q . {} \; -print | xargs sed -i "s|${srcdir}|/opt/pacbrew/dc|g"
  sed -i "s|/usr/local/dc-new/sh-elf/sh-elf/lib|/opt/pacbrew/dc/host/sh-elf/sh-elf/lib|g" $srcdir/KallistiOS-$_koscommit/utils/ldscripts/shlelf.xc

  # install
  install -d $pkgdir/opt/pacbrew/dc/target/sh-elf
  mv $srcdir/KallistiOS-$_koscommit $pkgdir/opt/pacbrew/dc/target/sh-elf/kos
}

