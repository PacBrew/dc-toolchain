# Maintainer: cpasjuste <cpasjuste@gmail.com>

_koscommit=c84b74b52c9240cd02fe52d5f92d4d256d85ce69
_dscommit=831f9875f8fca5f86735f675b73bc4294fde286c

pkgname=dc-kos
pkgver=2
pkgrel=1
pkgdesc='KOS is an unofficial development environment for the SEGA Dreamcast game console (with DreamShell patches).'
arch=('any')
url='https://github.com/KallistiOS/KallistiOS'
license=('MIT')
options=(!strip libtool)
source=(
  "https://github.com/KallistiOS/KallistiOS/archive/$_koscommit.tar.gz"
  "https://github.com/DC-SWAT/DreamShell/raw/$_dscommit/sdk/toolchain/patches/kos.diff"
)
sha256sums=(
  '802e95acfad50e7c6419814248dbe2a718b784c5182d43b426d545f08f739dfc'
  '4d3ae1f8f03ca74a473d92ea7eda14f43cdcd0b084ccfdcc990e84885b455dd8'
)
depends=('dc-environment')

prepare() {
  # apply ds patch
  cd $srcdir/KallistiOS-$_koscommit
  patch -p1 < $srcdir/kos.diff
}

build() {
  cd $srcdir/KallistiOS-$_koscommit

  # use source dir for building
  export KOS_BASE="$srcdir/KallistiOS-$_koscommit"
  
  # setup PacBrew toolchain
  source /opt/pacbrew/dc/dc-env
  
  # reset PacBrew defines for kos build
  AS="as" CC="gcc" CXX="g++" CPP="cpp" \
    LD="ld" AR="ar" NM="nm" RANLIB="ranlib" STRIP="strip" \
    CFLAGS="" CPPFLAGS="" CXXFLAGS="" LDFLAGS="" \
    make $MAKEFLAGS
}

package() {
  # cleanup
  find $srcdir/KallistiOS-$_koscommit -type f -name "*.o" -exec rm -f {} \;
  
  # fix prefix
  find $srcdir/KallistiOS-$_koscommit -type f -exec grep -I -q . {} \; -print | xargs sed -i "s|${srcdir}|/opt/pacbrew/dc|g"
  sed -i "s|/usr/local/dc-new/sh-elf/sh-elf/lib|/opt/pacbrew/dc/host/sh-elf/sh-elf/lib|g" $srcdir/KallistiOS-$_koscommit/utils/ldscripts/shlelf.xc

  # install
  install -d $pkgdir/opt/pacbrew/dc/target/sh-elf
  mv $srcdir/KallistiOS-$_koscommit $pkgdir/opt/pacbrew/dc/target/sh-elf/kos
}

